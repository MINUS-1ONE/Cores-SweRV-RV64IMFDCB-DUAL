// SPDX-License-Identifier: Apache-2.0
// Copyright 2019 Western Digital Corporation or its affiliates.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
// http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

// Run time disassembler functions
// supports  RISCV extentions I, C, M, A
`ifndef RV_NUM_THREADS
`define RV_NUM_THREADS 1
`endif

bit[31:0] [63:0] gpr[`RV_NUM_THREADS];

// main DASM function
// opcode: instruction code
// pc: address
// regn: write back gpr number
// regv: write back value to gpr
// tid: thread id
function string dasm(input[31:0] opcode, input[63:0] pc, input[4:0] regn, input[63:0] regv, input tid=0);
    dasm = (opcode[1:0] == 2'b11) ? dasm32(opcode, pc, tid) : dasm16(opcode, pc, tid);
    if(regn) gpr[tid][regn] = regv;
endfunction


///////////////// 16 bits instructions ///////////////////////

function string dasm16( input[31:0] opcode, input[63:0] pc, input tid=0);
    case(opcode[1:0])
    0: return dasm16_0(opcode, tid);
    1: return dasm16_1(opcode, pc);
    2: return dasm16_2(opcode);
    endcase
    return $sformatf(".short 0x%h", opcode[15:0]);
endfunction

function string dasm16_0( input[31:0] opcode, tid);
    case(opcode[15:13])
    3'b000: return dasm16_ciw(opcode);
    3'b001: return {"c.fld  ", dasm16_cl(opcode, tid)};
    3'b010: return {"c.lw   ", dasm16_cl(opcode, tid)};
    3'b011: return {"c.ld   ", dasm16_cl(opcode, tid)};
    3'b101: return {"c.fsd  ", dasm16_cl(opcode, tid)};
    3'b110: return {"c.sw   ", dasm16_cl(opcode, tid)};
    3'b111: return {"c.sd   ", dasm16_cl(opcode, tid)};
    endcase
    return $sformatf(".short  0x%h", opcode[15:0]);
endfunction

function string dasm16_ciw( input[31:0] opcode);
int imm;
    imm=0;
    if(opcode[15:0] == 0) return ".short  0";
    {imm[5:4],imm[9:6],imm[2],imm[3]} = opcode[12:5];
    return $sformatf("c.addi4spn %s,0x%0h", abi_reg[opcode[4:2]+8], imm);
endfunction

function string dasm16_cl( input[31:0] opcode, input tid=0);
int imm;
    imm=0;
    imm[5:3] = opcode[12:10];
    case(opcode[15:13])
    3'b010, 3'b110: begin //c.lw, c.sw
        imm[6] = opcode[5];
        imm[2] = opcode[6];
    end
    3'b001, 3'b011, 3'b101, 3'b111: imm[7:6] = opcode[6:5]; //c.fld, c.ld, c.fsd, c.sd
    endcase

    return $sformatf(" %s,%0d(%s) [%h]", abi_reg[opcode[4:2]+8], imm, abi_reg[opcode[9:7]+8], gpr[tid][opcode[9:7]+8]+imm);
endfunction

function string dasm16_1( input[31:0] opcode, input[63:0] pc);
    case(opcode[15:13])
    3'b000: return opcode[11:7]==0 ? "c.nop" : {"c.addi  ",dasm16_ci(opcode)};
    3'b001: return {"c.addiw ", dasm16_ci(opcode)};
    3'b010: return {"c.li    ", dasm16_ci(opcode)};
    3'b011: return dasm16_1_3(opcode);
    3'b100: return dasm16_cr(opcode);
    3'b101: return {"c.j     ", dasm16_cj(opcode, pc)};
    3'b110: return {"c.beqz  ", dasm16_cb(opcode, pc)};
    3'b111: return {"c.bnez  ", dasm16_cb(opcode, pc)};
    endcase
endfunction

function string dasm16_ci( input[31:0] opcode);
int imm;
    imm=0;
    imm[4:0] = opcode[6:2];
    if(opcode[12]) imm [31:5] = '1;
    return $sformatf("%s,%0d", abi_reg[opcode[11:7]], imm);
endfunction

function string dasm16_cj( input[31:0] opcode, input[63:0] pc);
bit[31:0] imm;
    imm=0;
    {imm[11],imm[4],imm[9:8],imm[10],imm[6], imm[7],imm[3:1], imm[5]} = opcode[12:2];
    if(opcode[12]) imm [31:12] = '1;
    return $sformatf("0x%0h", imm+pc);
endfunction

function string dasm16_cb( input[31:0] opcode, input[63:0] pc);
bit[31:0] imm;
    imm=0;
    {imm[8],imm[4:3]} = opcode[12:10];
    {imm[7:6],imm[2:1], imm[5]} = opcode[6:2];
    if(opcode[12]) imm [31:9] = '1;
    return $sformatf("%s,0x%0h",abi_reg[opcode[9:7]+8], imm+pc);
endfunction

function string dasm16_cr( input[31:0] opcode);
bit[31:0] imm;

    imm = 0;
    imm[4:0] = opcode[6:2];
    if(opcode[12]) imm [31:5] = '1;
    case(opcode[11:10])
    0: return $sformatf("c.srli  %s,%0d",  abi_reg[opcode[9:7]+8], imm[5:0]);
    1: return $sformatf("c.srai  %s,%0d",  abi_reg[opcode[9:7]+8], imm[5:0]);
    2: return $sformatf("c.andi  %s,0x%0h", abi_reg[opcode[9:7]+8], imm);
    endcase

//modified for RV64C
    if(opcode[12]) begin
        case(opcode[6:5])
        0: return $sformatf("c.subw  %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        1: return $sformatf("c.addw  %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        endcase
    end
    else begin
        case(opcode[6:5])
        0: return $sformatf("c.sub   %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        1: return $sformatf("c.xor   %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        2: return $sformatf("c.or    %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        3: return $sformatf("c.and   %s,%s", abi_reg[opcode[9:7]+8], abi_reg[opcode[4:2]+8]);
        endcase
    end
endfunction

function string dasm16_1_3( input[31:0] opcode);
int imm;

    imm=0;
    if(opcode[11:7] == 2) begin
        {imm[4], imm[6],imm[8:7], imm[5]} = opcode[6:2];
        if(opcode[12]) imm [31:9] = '1;
        return $sformatf("c.addi16sp %0d", imm);
    end
    else begin
        imm[16:12] = opcode[6:2];
        if(opcode[12]) imm [31:17] = '1;
        return $sformatf("c.lui   %s,0x%0h", abi_reg[opcode[11:7]], imm);

    end
endfunction

// moodified for RV64C
function string dasm16_2( input[31:0] opcode, input tid=0);
    case(opcode[15:13])
    3'b000: return {"c.slli  ", dasm16_ci(opcode)};
    3'b001: return {"c.fldsp ", dasm16_cls(opcode,1,tid)};
    3'b010: return {"c.lwsp  ", dasm16_cls(opcode,0,tid)};
    3'b011: return {"c.ldsp  ", dasm16_cls(opcode,1,tid)};
    3'b101: return {"c.fsdsp ", dasm16_css(opcode,1,tid)};
    3'b110: return {"c.swsp  ", dasm16_css(opcode,0,tid)};
    3'b111: return {"c.sdsp  ", dasm16_css(opcode,1,tid)};
    endcase
    if(opcode[12]) begin
        if(opcode[12:2] == 0) return "c.ebreak";
        else if(opcode[6:2] == 0) return $sformatf("c.jalr  %s", abi_reg[opcode[11:7]]);
        else return $sformatf("c.add   %s,%s", abi_reg[opcode[11:7]], abi_reg[opcode[6:2]]);
    end
    else begin
        if(opcode[6:2] == 0) return $sformatf("c.jr    %s", abi_reg[opcode[11:7]]);
        else return $sformatf("c.mv    %s,%s", abi_reg[opcode[11:7]], abi_reg[opcode[6:2]]);
    end
endfunction


function string dasm16_cls( input[31:0] opcode, input sh1=0, tid=0);
bit[31:0] imm;
    imm=0;
    if(sh1) {imm[4:3],imm[8:6]} = opcode[6:2];
    else    {imm[4:2],imm[7:6]} = opcode[6:2];
    imm[5] = opcode[12];
    return $sformatf("%s,0x%0h [%h]", abi_reg[opcode[11:7]], imm, gpr[tid][2]+imm);
endfunction

function string dasm16_css( input[31:0] opcode, input sh1=0, tid=0);
bit[31:0] imm;
    imm=0;
    if(sh1) {imm[5:3],imm[8:6]} = opcode[12:7];
    else {imm[5:2],imm[7:6]} = opcode[12:7];
    return $sformatf("%s,0x%0h [%h]", abi_reg[opcode[6:2]], imm, gpr[tid][2]+imm);
endfunction

///////////////// 32 bit instructions ///////////////////////

function string dasm32( input[31:0] opcode, input[63:0] pc, input tid=0);
    case(opcode[6:0])
    7'b0110111: return {"lui     ", dasm32_u(opcode)};
    7'b0010111: return {"auipc   ", dasm32_u(opcode)};
    7'b1101111: return {"jal     ", dasm32_j(opcode,pc)};
    7'b1100111: return {"jalr    ", dasm32_jr(opcode,pc)};
    7'b1100011: return dasm32_b(opcode,pc);
    7'b0000011: return dasm32_l(opcode,tid);
    7'b0100011: return dasm32_s(opcode,tid);
    7'b0010011: return dasm32_ai(opcode);
    7'b0011011: return dasm32_aiw(opcode);
    7'b0111011: return dasm32_arw(opcode);
    7'b0110011: return dasm32_ar(opcode);
    7'b0001111: return {"fence", dasm32_fence(opcode)};
    7'b1110011: return dasm32_e(opcode);
    7'b0101111: return dasm32_a(opcode,tid);
    // load-fp
    7'b0000111: return dasm32_fl(opcode,tid);
    // store-fp
    7'b0100111: return dasm32_fs(opcode,tid);
    // op-fp
    7'b1010011: return dasm32_op_fp(opcode);
    // FMADD
    7'b1000011: return dasm32_fma(opcode);
    // FMSUB
    7'b1000111: return dasm32_fma(opcode);
    // FNMSUB
    7'b1001011: return dasm32_fma(opcode);
    // FNMADD
    7'b1001111: return dasm32_fma(opcode);

    endcase
    return $sformatf(".long   0x%h", opcode);
endfunction

function string dasm32_u( input[31:0] opcode);
bit[31:0] imm;
    imm=0;
    imm[31:12] = opcode[31:12];
    return $sformatf("%s,0x%0h", abi_reg[opcode[11:7]], imm);
endfunction

function string dasm32_j( input[31:0] opcode, input[63:0] pc);
int imm;
    imm=0;
    {imm[20], imm[10:1], imm[11], imm[19:12]} = opcode[31:12];
    if(opcode[31]) imm[31:20] = '1;
    return $sformatf("%s,0x%0h",abi_reg[opcode[11:7]], imm+pc);
endfunction

function string dasm32_jr( input[31:0] opcode, input[63:0] pc);
int imm;
    imm=0;
    imm[11:1] = opcode[31:19];
    if(opcode[31]) imm[31:12] = '1;
    return $sformatf("%s,%s,0x%0h",abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], imm+pc);
endfunction

function string dasm32_b( input[31:0] opcode, input[63:0] pc);
int imm;
string mn;
    imm=0;
    {imm[12],imm[10:5]} = opcode[31:25];
    {imm[4:1],imm[11]} = opcode[11:7];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    0: mn = "beq     ";
    1: mn = "bne     ";
    2,3 : return $sformatf(".long    0x%h", opcode);
    4: mn = "blt     ";
    5: mn = "bge     ";
    6: mn = "bltu    ";
    7: mn = "bgeu    ";
    endcase
    return $sformatf("%s%s,%s,0x%0h", mn, abi_reg[opcode[19:15]], abi_reg[opcode[24:20]], imm+pc);
endfunction

function string dasm32_l( input[31:0] opcode, input tid=0);
int imm;
string mn;
    imm=0;
    imm[11:0] = opcode[31:20];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    0: mn = "lb      ";
    1: mn = "lh      ";
    2: mn = "lw      ";
    3: mn = "ld      ";
    4: mn = "lbu     ";
    5: mn = "lhu     ";
    6: mn = "lwu     ";
    default : return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%0d(%s) [%h]", mn, abi_reg[opcode[11:7]], imm, abi_reg[opcode[19:15]], imm+gpr[tid][opcode[19:15]]);
endfunction

function string dasm32_s( input[31:0] opcode, input tid=0);
int imm;
string mn;
    imm=0;
    imm[11:5] = opcode[31:25];
    imm[4:0] = opcode[11:7];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    0: mn = "sb      ";
    1: mn = "sh      ";
    2: mn = "sw      ";
    3: mn = "sd      ";
    default : return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%0d(%s) [%h]", mn, abi_reg[opcode[24:20]], imm, abi_reg[opcode[19:15]], imm+gpr[tid][opcode[19:15]]);
endfunction

function string dasm32_ai( input[31:0] opcode);
int imm;
string mn;
    imm=0;
    imm[11:0] = opcode[31:20];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    0: mn = "addi    ";
    2: mn = "slti    ";
    3: mn = "sltiu   ";
    4: mn = "xori    ";
    6: mn = "ori     ";
    7: mn = "andi    ";
    default: return dasm32_si(opcode);
    endcase

return $sformatf("%s%s,%s,%0d", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], imm);
endfunction

// added for RV64I
function string dasm32_aiw( input[31:0] opcode);
int imm;
string mn;
    imm=0;
    imm[5:0] = opcode[25:20];
    case(opcode[14:12])
    3'b000: begin
       mn = "addiw   ";
       imm[11:6] = opcode[31:26];
       imm[31:12] = {20{opcode[31]}};
    end
    3'b001: begin
        case(opcode[31:25])
        7'b0000000: mn = "slliw   ";
        7'b0000100,
        7'b0000101: mn = "slli.uw ";
        7'b0110000: begin
            case(opcode[24:20])
                5'b00000: mn = "clzw    ";
                5'b00010: mn = "cpopw   ";
                5'b00001: mn = "ctzw    ";
            endcase
            return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
        end
        endcase
    end
    3'b101: begin
        case(opcode[31:25])
        7'b0000000: mn = "srliw   ";
        7'b0100000: mn = "sraiw   ";
        7'b0110000: mn = "roriw   ";
        endcase
    end
    default: return $sformatf(".long   0x%h", opcode);
    endcase

return $sformatf("%s%s,%s,%0d", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], imm);
endfunction

function string dasm32_si( input[31:0] opcode);
int imm;
string mn;
    imm = opcode[25:20];
    case({opcode[31:20],opcode[14:12]})
    15'b001010000111_101: 
    begin
        mn = "orc.b   ";
        return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
    end
    15'b011010111000_101: 
    begin
        mn = "rev8    ";
        return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
    end
    endcase

    case({opcode[31:27],opcode[14:12]})
    8'b00000_001: mn = "slli    ";
    8'b01000_101: mn = "srai    ";
    8'b00000_101: mn = "srli    ";
    8'b01100_101: mn = "rori    ";
    8'b01001_001: mn = "bclri   ";
    8'b00101_001: mn = "bseti   ";
    8'b01101_001: mn = "binvi   ";
    8'b01001_101: mn = "bexti   ";
    8'b01100_001: return dasm32_cz(opcode);
    default: return $sformatf(".long   0x%h", opcode);
    endcase

    return $sformatf("%s%s,%s,%0d", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], imm);
endfunction

function string dasm32_cz( input[31:0] opcode);
string mn;
    case(opcode[24:20])
    5'b00000: mn = "clz     ";
    5'b00001: mn = "ctz     ";
    5'b00010: mn = "cpop    ";
    5'b00100: mn = "sext.b  ";
    5'b00101: mn = "sext.h  ";
    default: return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
endfunction

function string dasm32_ar( input[31:0] opcode);
string mn;
//         func7         func3
    case({opcode[31:25],opcode[14:12]})
    10'b0000001_000: mn = "mul     ";
    10'b0000001_001: mn = "mulh    ";
    10'b0000001_010: mn = "mulhsu  ";
    10'b0000001_011: mn = "mulhu   ";
    10'b0000001_100: mn = "div     ";
    10'b0000001_101: mn = "divu    ";
    10'b0000001_110: mn = "rem     ";
    10'b0000001_111: mn = "remu    ";
    10'b0100000_000: mn = "sub     ";
    10'b0000000_000: mn = "add     ";
    10'b0000000_001: mn = "sll     ";
    10'b0000000_010: mn = "slt     ";
    10'b0000000_011: mn = "sltu    ";
    10'b0000000_100: mn = "xor     ";
    10'b0100000_101: mn = "sra     ";
    10'b0000000_101: mn = "srl     ";
    10'b0000000_110: mn = "or      ";
    10'b0000000_111: mn = "and     ";
// bit manip:
    10'b0100000_111: mn = "andn    ";
    10'b0100000_110: mn = "orn     ";
    10'b0100000_100: mn = "xnor    ";
    10'b0110000_001: mn = "rol     ";
    10'b0110000_101: mn = "ror     ";
    10'b0010000_010: mn = "sh1add  ";
    10'b0010000_100: mn = "sh2add  ";
    10'b0010000_110: mn = "sh3add  ";
    10'b0100100_001: mn = "bclr    ";
    10'b0010100_001: mn = "bset    ";
    10'b0110100_001: mn = "binv    ";
    10'b0100100_101: mn = "bext    ";
    10'b0000101_001: mn = "clmul   ";
    10'b0000101_010: mn = "clmulr  ";
    10'b0000101_011: mn = "clmulh  ";
    10'b0000101_100: mn = "min     ";
    10'b0000101_101: mn = "minu    ";
    10'b0000101_110: mn = "max     ";
    10'b0000101_111: mn = "maxu    ";
    default: return $sformatf(".long  0x%h", opcode);
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], abi_reg[opcode[24:20]]);
endfunction

// added for RV64IM
function string dasm32_arw( input[31:0] opcode);
string mn;
    if({opcode[31:20],opcode[14:12]}==15'b001010000111_101)
    begin
        mn = "zext.h  ";
        return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
    end

    case({opcode[31:25],opcode[14:12]})
    //RV64I
    10'b0000000_000: mn = "addw    ";
    10'b0100000_000: mn = "subw    ";
    10'b0000000_001: mn = "sllw    ";
    10'b0000000_101: mn = "srlw    ";
    10'b0100000_101: mn = "sraw    ";
    //RV64M
    10'b0000001_000: mn = "mulw    ";
    10'b0000001_100: mn = "divw    ";
    10'b0000001_101: mn = "divuw   ";
    10'b0000001_110: mn = "remw    ";
    10'b0000001_111: mn = "remuw   ";
    //RV64B
    10'b0000100_000: mn = "add.uw  ";
    10'b0110000_001: mn = "rolw    ";
    10'b0110000_101: mn = "rorw    ";
    10'b0010000_010: mn = "sh1add.uw ";
    10'b0010000_100: mn = "sh2add.uw ";
    10'b0010000_110: mn = "sh3add.uw ";
    default: return $sformatf(".long  0x%h", opcode);
    endcase

    return $sformatf("%s%s,%s,%s", mn, abi_reg[opcode[11:7]], abi_reg[opcode[19:15]], abi_reg[opcode[24:20]]);
endfunction

function string dasm32_fence( input[31:0] opcode);
    return  opcode[12] ? ".i" : "";
endfunction

function string dasm32_e(input[31:0] opcode);
    if(opcode[31:0] == 32'b00110000001000000000000001110011) return "mret";
    else if(opcode[31:0] == 32'b00010000010100000000000001110011) return "wfi";
    else if(opcode[31:7] == 0) return "ecall";
    else if({opcode[31:21],opcode [19:7]} == 0) return "ebreak";
    else
        case(opcode[14:12])
        1: return {"csrrw   ", dasm32_csr(opcode)};
        2: return {"csrrs   ", dasm32_csr(opcode)};
        3: return {"csrrc   ", dasm32_csr(opcode)};
        5: return {"csrrwi  ", dasm32_csr(opcode, 1)};
        6: return {"csrrsi  ", dasm32_csr(opcode, 1)};
        7: return {"csrrci  ", dasm32_csr(opcode, 1)};
        endcase

endfunction


function string dasm32_csr(input[31:0] opcode, input im=0);
bit[11:0] csr;
    csr = opcode[31:20];
    if(im) begin
        return $sformatf("%s,csr_%0h,0x%h",  abi_reg[opcode[11:7]], csr, opcode[19:15]);
    end
    else begin
        return $sformatf("%s,csr_%0h,%s",  abi_reg[opcode[11:7]], csr, abi_reg[opcode[19:15]]);
    end

endfunction

//atomics
function string dasm32_a(input[31:0] opcode, input tid=0);
    case(opcode[31:27])
    'b00010: return $sformatf("lr.w    %s,(%s) [%h]",    abi_reg[opcode[11:7]],                         abi_reg[opcode[19:15]], gpr[tid][opcode[19:15]]);
    'b00011: return $sformatf("sc.w    %s,%s,(%s) [%h]", abi_reg[opcode[11:7]], abi_reg[opcode[24:20]], abi_reg[opcode[19:15]], gpr[tid][opcode[19:15]]);
    'b00001: return {"amoswap.w", dasm32_amo(opcode, tid)};
    'b00000: return {"amoadd.w",  dasm32_amo(opcode, tid)};
    'b00100: return {"amoxor.w",  dasm32_amo(opcode, tid)};
    'b01100: return {"amoand.w",  dasm32_amo(opcode, tid)};
    'b01000: return {"amoor.w",   dasm32_amo(opcode, tid)};
    'b10000: return {"amomin.w",  dasm32_amo(opcode, tid)};
    'b10100: return {"amomax.w",  dasm32_amo(opcode, tid)};
    'b11000: return {"amominu.w", dasm32_amo(opcode, tid)};
    'b11100: return {"amomaxu.w", dasm32_amo(opcode, tid)};
    endcase
    return $sformatf(".long 0x%h", opcode);
endfunction

function string dasm32_amo( input[31:0] opcode, input tid=0);
    return $sformatf(" %s,%s,(%s) [%h]", abi_reg[opcode[11:7]], abi_reg[opcode[24:20]], abi_reg[opcode[19:15]], gpr[tid][opcode[19:15]]);
endfunction

function string dasm32_fl( input[31:0] opcode, input tid=0);
int imm;
string mn;
    imm=0;
    imm[11:0] = opcode[31:20];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    3'b010: mn = "flw      ";
    3'b011: mn = "fld      ";
    default : return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%0d(%s) [%h]", mn, abi_f_reg[opcode[11:7]], imm, abi_reg[opcode[19:15]], imm+gpr[tid][opcode[19:15]]);
endfunction

function string dasm32_fs( input[31:0] opcode, input tid=0);
int imm;
string mn;
    imm=0;
    imm[11:5] = opcode[31:25];
    imm[4:0] = opcode[11:7];
    if(opcode[31]) imm[31:12] = '1;
    case(opcode[14:12])
    3'b010: mn = "fsw      ";
    3'b010: mn = "fsd      ";
    default : return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%0d(%s) [%h]", mn, abi_f_reg[opcode[24:20]], imm, abi_reg[opcode[19:15]], imm+gpr[tid][opcode[19:15]]);
endfunction

function string dasm32_fma( input[31:0] opcode);
string mn;
    case(opcode[26:25])
    2'b00:
        case(opcode[4:2])
        3'b000: mn = "fmadd.s   ";
        3'b001: mn = "fmsub.s   ";
        3'b010: mn = "fnmsub.s  ";
        3'b011: mn = "fnmadd.s  ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    2'b01:
        case(opcode[4:2])
        3'b000: mn = "fmadd.d   ";
        3'b001: mn = "fmsub.d   ";
        3'b010: mn = "fnmsub.d  ";
        3'b011: mn = "fnmadd.d  ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    default : return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]], abi_f_reg[opcode[31:27]]);
endfunction

function string dasm32_op_fp( input[31:0] opcode);
string mn;
    casex(opcode[31:25])
    7'b000000x: return dasm32_fadd(opcode);
    7'b000010x: return dasm32_fsub(opcode);
    7'b000100x: return dasm32_fmul(opcode);
    7'b000110x: return dasm32_fdiv(opcode);
    7'b010110x: return dasm32_fsqrt(opcode);
    7'b001000x: return dasm32_fsgn(opcode);
    7'b001010x: return dasm32_fmaxmin(opcode);
    7'b110000x: return dasm32_fcvt_ftox(opcode);
    7'b110100x: return dasm32_fcvt_xtof(opcode);
    7'b010000x: return dasm32_fcvt_ftof(opcode);
    7'b111000x: return dasm32_f_uni_type(opcode);
    7'b111100x: return dasm32_fmv_xtof(opcode);
    7'b101000x: return dasm32_fcmp(opcode);
    endcase
    return $sformatf(".long   0x%h", opcode);
endfunction

function string dasm32_f_uni_type(input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: 
            case (opcode[14:12])
            3'b000: mn = "fmv.x.w   ";
            3'b001: mn = "fclass.s   ";
            default: return $sformatf(".long   0x%h", opcode);
            endcase
        1'b1: 
            case (opcode[14:12])
            3'b000: mn = "fmv.x.d   ";
            3'b001: mn = "fclass.d   ";
            default: return $sformatf(".long   0x%h", opcode);
            endcase
    endcase
    return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_f_reg[opcode[19:15]]);
endfunction

function string dasm32_fmv_xtof(input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fmv.w.x   ";
        1'b1: mn = "fmv.d.x   ";
        default: return $sformatf(".long   0x%h", opcode);
    endcase
    return $sformatf("%s%s,%s", mn, abi_f_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
endfunction

function string dasm32_fcmp( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[14:12])
        3'b000 : mn = "fle.s   ";
        3'b001 : mn = "flt.s   ";
        3'b010 : mn = "feq.s   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[14:12])
        3'b000 : mn = "fle.d   ";
        3'b001 : mn = "flt.d   ";
        3'b010 : mn = "feq.d   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fcvt_ftox( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[24:20])
        5'b00000: mn = "fcvt.w.s   ";
        5'b00001: mn = "fcvt.wu.s   ";
        5'b00010: mn = "fcvt.l.s   ";
        5'b00011: mn = "fcvt.lu.s   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[24:20])
        5'b00000: mn = "fcvt.w.d   ";
        5'b00001: mn = "fcvt.wu.d   ";
        5'b00010: mn = "fcvt.l.d   ";
        5'b00011: mn = "fcvt.lu.d   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s", mn, abi_reg[opcode[11:7]], abi_f_reg[opcode[19:15]]);
endfunction

function string dasm32_fcvt_xtof( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[24:20])
        5'b00000: mn = "fcvt.s.w   ";
        5'b00001: mn = "fcvt.s.wu   ";
        5'b00010: mn = "fcvt.s.l   ";
        5'b00011: mn = "fcvt.s.lu   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[24:20])
        5'b00000: mn = "fcvt.d.w   ";
        5'b00001: mn = "fcvt.d.wu   ";
        5'b00010: mn = "fcvt.d.l   ";
        5'b00011: mn = "fcvt.d.lu   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s", mn, abi_f_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
endfunction

function string dasm32_fcvt_ftof( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[24:20])
        5'b00001: mn = "fcvt.s.d   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[24:20])
        5'b00000: mn = "fcvt.d.s   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]]);
endfunction

function string dasm32_fcvt_s_w( input[31:0] opcode);
string mn;
    case (opcode[20])
    1'b0 : mn = "fcvt.s.w   ";
    1'b1 : mn = "fcvt.s.wu   ";
    endcase
    return $sformatf("%s%s,%s", mn, abi_f_reg[opcode[11:7]], abi_reg[opcode[19:15]]);
endfunction

function string dasm32_fmaxmin( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[14:12])
        3'b000 : mn = "fmin.s   ";
        3'b001 : mn = "fmax.s   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[14:12])
        3'b000 : mn = "fmin.d   ";
        3'b001 : mn = "fmax.d   ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fsgn( input[31:0] opcode);
string mn;
    case (opcode[25])
    1'b0:
        case (opcode[14:12])
        3'b000 : mn = "fsgnj.s   ";
        3'b001 : mn = "fsgnjn.s   ";
        3'b010 : mn = "fsgnjx.s  ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    1'b1:
        case (opcode[14:12])
        3'b000 : mn = "fsgnj.d   ";
        3'b001 : mn = "fsgnjn.d   ";
        3'b010 : mn = "fsgnjx.d  ";
        default : return $sformatf(".long   0x%h", opcode);
        endcase
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fadd( input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fadd.s   ";
        1'b1: mn = "fadd.d   ";
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fsub( input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fsub.s   ";
        1'b1: mn = "fsub.d   ";
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fmul( input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fmul.s   ";
        1'b1: mn = "fmul.d   ";
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fdiv( input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fdiv.s   ";
        1'b1: mn = "fdiv.d   ";
    endcase
    return $sformatf("%s%s,%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]], abi_f_reg[opcode[24:20]]);
endfunction

function string dasm32_fsqrt( input[31:0] opcode);
string mn;
    case (opcode[25])
        1'b0: mn = "fsqrt.s   ";
        1'b1: mn = "fsqrt.d   ";
    endcase
    return $sformatf("%s%s,%s", mn, abi_f_reg[opcode[11:7]], abi_f_reg[opcode[19:15]]);
endfunction
